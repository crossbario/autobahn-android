def IS_ANDROID = project.hasProperty('IS_ANDROID') ? project.IS_ANDROID: true
def IS_NEXT = project.hasProperty('IS_NEXT') ? project.IS_NEXT: false
def IS_NETTY = project.hasProperty('IS_NETTY') ? project.IS_NETTY: false
def ARTIFACT_ANDROID = (project.hasProperty('BUILD_LEGACY') && project.BUILD_LEGACY) ? 'autobahn-android-legacy': 'autobahn-android'
def PUBLISH = project.hasProperty('PUBLISH') && project.PUBLISH

apply plugin: IS_ANDROID ? 'com.android.library': 'java-library'
apply plugin: 'maven-publish'

def ARTIFACT_JAVA = 'autobahn-java'
def ARTIFACT_NEXT = 'autobahn'

def groupID = 'io.crossbar.autobahn'
def gitUrl = 'https://github.com/crossbario/autobahn-java.git'
def licenseName = 'MIT'
def licenseUrl = 'https://opensource.org/licenses/MIT'
def relVersion = System.getenv().containsKey('AUTOBAHN_BUILD_VERSION') ? System.getenv(
        'AUTOBAHN_BUILD_VERSION'): '21.4.24'
def siteUrl = 'https://github.com/crossbario/autobahn-java'

dependencies {
    api 'com.fasterxml.jackson.core:jackson-core:2.12.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.12.3'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.8.22'
    if (IS_ANDROID) {
        implementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:2.0.2'
        implementation 'org.web3j:core:4.6.0-android'
        implementation 'org.web3j:abi:4.6.0-android'
        implementation 'org.web3j:utils:4.6.0-android'
    } else {
        implementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni:2.0.2'
        implementation 'org.web3j:core:4.6.0'
        implementation 'org.web3j:abi:4.6.0'
        implementation 'org.web3j:utils:4.6.0'
        implementation 'org.json:json:20210307'
    }
    if (IS_NETTY) {
        implementation 'io.netty:netty-codec-http:4.1.63.Final'
        implementation 'io.netty:netty-handler:4.1.63.Final'
    }
}

if (IS_ANDROID) {
    android {
        compileSdkVersion 30
        buildToolsVersion '30.0.3'
        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 30
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            }
        }
        sourceSets {
            main {
                java {
                    exclude 'io/crossbar/autobahn/wamp/transports/Netty*'
                    exclude 'io/crossbar/autobahn/utils/ABJLogger.java'
                }
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        lintOptions {
            abortOnError false
        }
        packagingOptions {
            exclude 'META-INF/spring.tooling'
            exclude 'META-INF/spring.handlers'
            exclude 'META-INF/spring.schemas'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/notice.txt'
        }
        dexOptions {
            javaMaxHeapSize "4g"
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set("sources")
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }
    afterEvaluate {
        javadoc.classpath += files(android.libraryVariants.collect { variant ->
            variant.javaCompile.classpath.files
        })
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    project.archivesBaseName = ARTIFACT_ANDROID
    group = groupID
    version = relVersion

    afterEvaluate {
        publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from components.release

                    artifact sourcesJar
                    artifact javadocJar

                    // You can then customize attributes of the publication as shown below.
                    groupId = groupID
                    artifactId = ARTIFACT_ANDROID
                    version = relVersion

                }
                // Creates a Maven publication called “debug”.
                debug(MavenPublication) {
                    // Applies the component for the debug build variant.
                    from components.debug

                    artifact sourcesJar
                    artifact javadocJar

                    groupId = groupID
                    artifactId = ARTIFACT_ANDROID + '-debug'
                    version = relVersion
                }
            }
        }
        repositories {
            maven {
                url 'https://repo1.maven.org/maven2'
                credentials {
                    username ""
                    password ""
                }
            }
        }
    }
} else {
    // Create the pom configuration:
    def pomConfig = {
        // Set your license
        licenses {
            license {
                name licenseName
                url licenseUrl
            }
        }
        developers {
            developer {
                id "crossbario"
                name "Crossbar.io"
                email "support@crossbario.com"
            }
        }
        scm {
            url siteUrl
        }
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                groupId groupID
                artifactId IS_NEXT ? ARTIFACT_NEXT: ARTIFACT_JAVA
                version relVersion
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'WebSocket & WAMP for Java8+')
                    root.appendNode('name', 'Autobahn Java')
                    root.appendNode('url', siteUrl)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    if (IS_NEXT) {
        sourceSets {
            main {
                java {
                    exclude 'io/crossbar/autobahn/wamp/transports/AndroidWebSocket.java'
                    exclude 'io/crossbar/autobahn/wamp/transports/Netty*'
                    exclude 'io/crossbar/autobahn/websocket/utils/AndroidThreadMessenger.java'
                    exclude 'io/crossbar/autobahn/utils/ABALogger.java'
                }
            }
        }
    } else {
        sourceSets {
            main {
                java {
                    exclude 'io/crossbar/autobahn/wamp/transports/AndroidWebSocket.java'
                    exclude 'io/crossbar/autobahn/wamp/transports/WebSocket.java'
                    exclude 'io/crossbar/autobahn/websocket'
                    exclude 'io/crossbar/autobahn/utils/ABALogger.java'
                }
            }
        }
    }
    jar {
        version = relVersion
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
