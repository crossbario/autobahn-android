apply plugin: project.PLATFORM == project.PLATFORM_NETTY ? project.PLUGIN_JAVA_APP : project.PLUGIN_ANDROID_APP

if (plugins.hasPlugin(project.PLUGIN_ANDROID_APP)) {
    android {
        compileSdkVersion 26
        buildToolsVersion '26.0.1'

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 26
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        sourceSets {
            main {
                java {
                    include 'io/crossbar/autobahn/demogallery/android/**'
                    include 'io/crossbar/autobahn/demogallery/ExampleClient.java'
                    exclude 'io/crossbar/autobahn/demogallery/netty/**'
                }
            }
        }
    }

    dependencies {
        implementation project(path: ':autobahn')
        implementation 'com.android.support:appcompat-v7:26.1.0'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.8.8'
        // implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.6'
    }
} else {
    mainClassName = 'io.crossbar.autobahn.demogallery.netty.Main'
    dependencies {
        implementation project(path: ':autobahn')
    }
    sourceSets {
        main {
            java {
                include 'io/crossbar/autobahn/demogallery/netty/**'
                include 'io/crossbar/autobahn/demogallery/ExampleClient.java'
                exclude 'io/crossbar/autobahn/demogallery/android/**'
            }
        }
    }
    jar {
        version = project.properties.get("buildVersion", "")
    }
}
